buildscript {
    repositories {
		mavenCentral()
    	maven {
			url 'http://repo.spring.io/libs-release'
    	}
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE'
		classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
    }
}
plugins {
	id "java"
	id "eclipse"
	id "com.bmuschko.vagrant" version "2.0"
}

apply plugin: 'spring-boot'

// Test Structure
sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    finalizedBy vagrantDestroy
}


jar {
    baseName = 'vagrant-poc'
    version =  '0.1.0'
    manifest {
        attributes 'Implementation-Title': 'Entire application lifecycle with Vagrant proof of concept', 
		'Implementation-Version': version,
		'Main-Class': 'hello.Application'
		
    }
}
ext {
	vagrant = 'vagrant'
}

repositories {
    mavenCentral()
    maven {
		url 'http://repo.spring.io/libs-release'
	}
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.2.2.RELEASE'

    // tag::actuator[]
    compile 'org.springframework.boot:spring-boot-starter-actuator:1.2.2.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.2.2.RELEASE'

    // end::actuator[]
    // tag::tests[]    
    integTestCompile 'org.springframework:spring-web:4.1.0.RELEASE'
    integTestCompile 'org.hamcrest:hamcrest-all:1.3'
    integTestCompile 'junit:junit:4.12'
    
    // end::tests[]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}


vagrant {
    boxDir = file(vagrant)

    environmentVariables {
        variable 'IP', '127.0.0.1'
        variable 'OPERATINGSYSTEM', 'redhat'
    }

    installation {
        validate = false
    }
}

import com.bmuschko.gradle.vagrant.tasks.Vagrant
import com.bmuschko.gradle.vagrant.tasks.VagrantSsh

task vagrantListsBoxes(type: Vagrant) {
    description = 'Outputs a list of available Vagrant boxes.'
    commands = ['box', 'list']
}


task vagrantRunApp(type: VagrantSsh) {
    description = 'Run the application via remote SSH command in Vagrant box.'
    //see: https://github.com/mitchellh/vagrant/issues/1553
    sshCommand = "nohup java -jar /vagrant_data/vagrant-poc-0.1.0.jar 0<&- &>/dev/null &"
    dependsOn vagrantUp    
}

vagrantRunApp.doLast {
	sleep(40 * 1000)
}

integTest.dependsOn(vagrantRunApp)
check.dependsOn integTest